#version 330 // OpenGL version 2.1 (minimum requirement)

in vec3 vertices; // Value sent from application to this attribute
in vec3 normals; // input color from application
in vec2 texCoords; // input value of texture coordinates
in vec3 tangents;
in vec3 bitangents;
layout (location=10) in mat4 instanceMatrix;

uniform vec3 CameraPosition;
uniform mat4 World;
uniform mat4 WVP;
uniform bool EnableInstancing = false;

out vec3 retNormal; // output a color to the fragment shader
out vec2 retTexCoord; // output texture coordinates (just a passthrough variable)
out vec3 retFragPos;
out vec3 retViewDirection;
out mat3 retTBN;

void main()
{
	mat4 world = World;
	if (EnableInstancing)
	{
		world *= instanceMatrix;
		gl_Position = WVP * instanceMatrix * vec4(vertices, 1.0);
	}
	else
	{
		gl_Position = WVP * vec4(vertices, 1.0);
	}

	retNormal = normalize(mat3(transpose(inverse(world))) * normals); // convert normals to world position
	/*	
		we cannot just multiply a normal by world matrix; 
		were using a normal matrix which is defined as the transpose of the inverse of the upper-left 3x3 part of the model matrix; 
		this avoids issues with non-uniform scaling affecting the normal so that it no longer is correct;
		additionally, it will not transpose correctly as a normal is simple a direction without a position
	*/
	retTexCoord = texCoords;
	retFragPos = vec3(world * vec4(vertices, 1.0f)); // convert fragments to world positions
	retViewDirection = normalize(CameraPosition - retFragPos);
	
	vec3 T = normalize(vec3(world * vec4(tangents, 0.0)));
	vec3 B = normalize(vec3(world * vec4(bitangents, 0.0)));
	vec3 N = normalize(vec3(world * vec4(normals, 0.0)));
	retTBN = mat3(T, B, N);
}